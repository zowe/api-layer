buildscript {
    repositories mavenRepositories
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:${gradleGitPropertiesVersion}")
        classpath ("org.eclipse.jgit:org.eclipse.jgit:5.13.0.202109080827-r") {
            force = true
        }
    }
}

normalization {
    runtimeClasspath {
        ignore("**/*git.properties*")
        ignore("**/*build-info.properties*")
    }
}

apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.gorylenko.gradle-git-properties'
apply from: "$projectDir/gradle/lite.gradle"
apply plugin: 'com.google.cloud.tools.jib'
apply from: "../gradle/jib.gradle"

def debugPort = 5120
def componentName = "gateway-service"

setJib(componentName, debugPort)
jib.container.mainClass = 'org.zowe.apiml.gateway.GatewayApplication'

springBoot {
    // This statement tells the Gradle Spring Boot plugin to generate a file
    // build/resources/main/META-INF/build-info.properties that is picked up by Spring Boot to display
    // via /info endpoint
    buildInfo {
        properties {
            // Generate extra build info:
            additionalProperties = [
                by             : System.properties['user.name'],
                operatingSystem: "${System.properties['os.name']} (${System.properties['os.version']})",
                number         : System.getenv('BUILD_NUMBER') ? System.getenv('BUILD_NUMBER') : "n/a",
                machine        : InetAddress.localHost.hostName
            ]
        }
    }
}

gitProperties {
    gitPropertiesDir = new File("${project.rootDir}/${name}/build/resources/main/META-INF")
}

configurations.all {
    resolutionStrategy {
        force (libraries.jetty_websocket_client)
    }
}

dependencies {
    implementation(project(':apiml-security-common'))
    implementation libraries.spring_boot_starter_webflux
    implementation libraries.spring_cloud_starter_zuul
    implementation libraries.spring_boot_starter_actuator
    implementation libraries.spring_boot_starter_web
    implementation libraries.spring_boot_starter_websocket
    implementation libraries.spring_boot_starter_thymeleaf
    implementation libraries.spring_boot_starter_cache
    implementation libraries.spring_boot_starter_aop
    implementation libraries.spring_cloud_starter_hystrix
    implementation libraries.spring_security_web
    implementation libraries.spring_security_config
    implementation libraries.spring_security_core
    implementation libraries.spring_security_crypto
    implementation libraries.xstream
    implementation libraries.json_smart

    implementation libraries.swagger3_core
    implementation libraries.swagger3_parser
    implementation libraries.jackson_core
    implementation libraries.tomcat_coyote
    implementation libraries.tomcat_annotations_api
    implementation libraries.tomcat_embed_core
    implementation libraries.tomcat_embed_websocket
    implementation libraries.spring_cloud_starter_eureka
    implementation libraries.spring_cloud_starter_ribbon
    implementation libraries.jetty_websocket_common
    implementation libraries.jetty_websocket_client
    implementation libraries.jetty_client
    implementation libraries.jetty_http
    implementation libraries.jetty_io
    implementation libraries.jetty_util
    implementation libraries.jetty_websocket_api
    implementation libraries.jjwt
    implementation libraries.nimbusJoseJwt
    implementation libraries.netty
    implementation libraries.netty_buffer
    implementation libraries.netty_codec
    implementation libraries.netty_codec_dns
    implementation libraries.netty_codec_haproxy
    implementation libraries.netty_codec_http
    implementation libraries.netty_codec_http2
    implementation libraries.netty_codec_memcache
    implementation libraries.netty_codec_mqtt
    implementation libraries.netty_codec_redis
    implementation libraries.netty_codec_socks
    implementation libraries.netty_codec_smtp
    implementation libraries.netty_codec_stomp
    implementation libraries.netty_codec_xml
    implementation libraries.netty_common
    implementation libraries.netty_handler
    implementation libraries.netty_handler_proxy
    implementation libraries.netty_resolver
    implementation libraries.netty_resolver_dns
    implementation libraries.netty_resolver_dns_native_macos
    implementation libraries.netty_transport
    implementation libraries.netty_transport_native_epoll
    implementation libraries.netty_transport_native_kqueue
    implementation libraries.netty_transport_native_unix_common
    implementation libraries.netty_transport_rxtx
    implementation libraries.netty_transport_sctp
    implementation libraries.netty_transport_udt

    implementation libraries.commons_io
    implementation libraries.guava
    implementation libraries.spring_aop
    implementation libraries.spring_beans
    implementation libraries.spring_context
    implementation libraries.spring_context_support
    implementation libraries.spring_core
    implementation libraries.spring_expression
    implementation libraries.spring_messaging
    implementation libraries.spring_web
    implementation libraries.spring_webmvc
    implementation libraries.spring_websocket

    implementation libraries.eh_cache
    implementation libraries.spring_retry
    implementation libraries.awaitility

    compileOnly libraries.javax_inject
    compileOnly libraries.lombok
    annotationProcessor libraries.lombok

    implementation libraries.springFox
    testCompile libraries.spring_mock_mvc
    testCompile libraries.spring_boot_starter_test
    testCompile libraries.json_smart
    testRuntime libraries.http_client
    testCompile libraries.rest_assured
    testCompile libraries.javax_inject

    testCompile libraries.lombok
    testAnnotationProcessor libraries.lombok


    runtime libraries.jjwt_impl
    runtime libraries.jjwt_jackson
}

bootJar {
    archiveFileName = "gateway-service.jar"
    manifest {
        attributes 'Main-Class': 'org.springframework.boot.loader.PropertiesLauncher'
    }
    archiveClassifier = "application"
}

jar {
    enabled true
}

bootRun {
    if (project.hasProperty('args')) {
        args project.args.split(',')
    }
    systemProperties = System.properties
}

publishing {
    publications {
        mavenJavaFat(MavenPublication) {
            groupId = 'org.zowe.apiml'
            artifactId = 'gateway-fat-jar'

            artifact bootJar
        }
    }
}
