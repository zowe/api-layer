buildscript {
    repositories {
        maven {
            setUrl('https://plugins.gradle.org/m2/')
        }
    }
    dependencies {
        classpath 'com.adarshr:gradle-test-logger-plugin:1.4.0'
    }
}

apply plugin: 'com.adarshr.test-logger'

repositories {
    mavenCentral()
}

dependencies {
    compile libraries.http_client
    compile libraries.eureka_client
    compile libraries.jackson_dataformat_yaml
    compile libraries.jackson_databind
    compile libraries.json_path
    compile libraries.spring_boot_starter_websocket
    compile libraries.json
    compile libraries.jjwt

    testCompile libraries.junit
    testCompile libraries.hamcrest
    testCompile libraries.rest_assured
    testCompile libraries.awaitility
    testCompile libraries.logback_classic
    testCompile libraries.powermock_api_mockito2
    testCompile libraries.power_mock_junit4
    testCompile libraries.power_mock_junit4_rule
    testCompile libraries.lombok
    testCompile libraries.jsoup
    testCompile libraries.rest_assured
}

jar {
    baseName = "integration-tests"
}

testlogger {
    theme 'standard-parallel'
}

test.enabled = false

apply plugin : 'java'

test {
    testLogging.showStandardStreams = true
}

task startUpCheck(type: Test) {
    group 'integration test'
    description "Check that the API Mediation Layer is up and runnig"

    systemProperties System.properties
    useJUnit {
        includeCategories 'com.ca.mfaas.util.categories.StartupCheck'
    }
    outputs.upToDateWhen { false }
}

task runWebsocketTests(type: Test) {
    group "integration test"
    description "Runs tests for WebSockets"

    systemProperties System.properties
    useJUnit {
        includeCategories 'com.ca.mfaas.util.categories.WebsocketTest'
    }
    outputs.upToDateWhen { false }
}

task runIntegrationTests(type: Test) {
    group "Integration tests"
    description "Run only tests without long tests"

    dependsOn startUpCheck
    systemProperties System.properties
    useJUnit {
        excludeCategories(
            'com.ca.mfaas.util.categories.StartupCheck',
            'com.ca.mfaas.util.categories.SlowTests',
            'com.ca.mfaas.util.categories.AdditionalLocalTest'
        )
    }
}

task runLocalIntegrationTests(type: Test) {
    group "Integration tests"
    description "Run integration tests that are not dependent on mainframe"

    dependsOn startUpCheck
    systemProperties System.getProperties()
    useJUnit {
        excludeCategories(
            'com.ca.mfaas.util.categories.StartupCheck',
            'com.ca.mfaas.util.categories.SlowTests',
            'com.ca.mfaas.util.categories.MainframeDependentTests',
            'com.ca.mfaas.util.categories.AdditionalLocalTest'
        )
    }
    outputs.upToDateWhen { false }
}

task runAllLocalIntegrationTests(type: Test) {
    group "Integration tests"
    description "Run integration tests for additional local services"

    dependsOn startUpCheck
    systemProperties System.getProperties()
    useJUnit {
        excludeCategories(
            'com.ca.mfaas.util.categories.StartupCheck',
            'com.ca.mfaas.util.categories.SlowTests',
            'com.ca.mfaas.util.categories.MainframeDependentTests'
        )
    }
    outputs.upToDateWhen { false }
}

task runAllIntegrationTests(type: Test) {
    group "Integration tests"
    description "Run all integration tests"

    dependsOn startUpCheck
    systemProperties System.properties
    useJUnit {
        excludeCategories(
            'com.ca.mfaas.util.categories.StartupCheck',
            'com.ca.mfaas.util.categories.AdditionalLocalTest'
        )
    }
    outputs.upToDateWhen { false }
}
