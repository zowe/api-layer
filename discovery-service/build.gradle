buildscript {
    repositories mavenRepositories
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:${gradleGitPropertiesVersion}")
        classpath ("org.eclipse.jgit:org.eclipse.jgit:5.13.3.202401111512-r") {
            force = true
        }
    }
}

normalization {
    runtimeClasspath {
        ignore("**/*git.properties*")
        ignore("**/*build-info.properties*")
    }
}

apply plugin: 'org.springframework.boot'
apply plugin: 'com.gorylenko.gradle-git-properties'
apply from: "$projectDir/gradle/lite.gradle"
apply plugin: 'com.google.cloud.tools.jib'
apply from: "../gradle/jib.gradle"

def debugPort = 5121
def componentName = "discovery-service"

setJib(componentName, debugPort)
jib.container.args.add('--spring.profiles.active=https')

springBoot {
    // This statement tells the Gradle Spring Boot plugin to generate a file
    // build/resources/main/META-INF/build-info.properties that is picked up by Spring Boot to display
    // via /info endpoint
    buildInfo {
        properties {
            // Generate extra build info:
            additionalProperties = [
                by: System.properties['user.name'],
                operatingSystem: "${System.properties['os.name']} (${System.properties['os.version']})",
                number: System.getenv('BUILD_NUMBER') ? System.getenv('BUILD_NUMBER') : "n/a",
                machine: InetAddress.localHost.hostName
            ]
        }
    }
}

gitProperties {
    gitPropertiesDir = new File("${project.rootDir}/${name}/build/resources/main/META-INF")
}

configurations.all {
    resolutionStrategy {
        force (libraries.jetty_websocket_client)
    }
}

dependencies {
    api(project(':security-service-client-spring')) {
        exclude group: "javax.servlet", module: "servlet-api"
    }
    implementation(libraries.spring_boot_starter_web) {
        exclude group: "org.springframework", module: "spring-web"
        exclude group: "org.springframework", module: "spring-webmvc"
        exclude group: "org.springframework", module: "spring-context"
        exclude group: "org.apache.tomcat.embed", module: "tomcat-embed-core"
        exclude group: "org.apache.tomcat.embed", module: "tomcat-embed-el"
        exclude group: "org.yaml", module: "snakeyaml"
        exclude group: "org.apache.tomcat.embed", module: "tomcat-embed-websocket"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-annotations"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-core"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-databind"
    }

    implementation libraries.snakeyaml
    implementation libraries.tomcat_embed_el
    implementation libraries.spring_boot_starter_websocket
    implementation libraries.spring_boot_starter_security
    implementation(libraries.spring_boot_starter_actuator) {
        exclude group: "org.springframework", module: "spring-context"
        exclude group: "org.springframework", module: "spring-core"
    }
    implementation libraries.spring_cloud_commons
    implementation(libraries.spring_cloud_starter_bootstrap) {
        transitive false
    }
    implementation libraries.spring_cloud_starter_hystrix
    implementation(libraries.spring_cloud_starter_eureka_server)  {
        exclude group: "javax.servlet", module: "servlet-api"
        exclude group: "com.google.code.gson", module: "gson"
        exclude group: "com.netflix.eureka", module: "eureka-core"
    }
    implementation(libraries.eureka_core) {
        exclude group: "com.amazonaws", module: "aws-java-sdk-core"
        exclude group: "javax.servlet", module: "servlet-api"
    }
    implementation libraries.aws_sdk_core
    implementation libraries.gson
    implementation libraries.jackson_annotations
    implementation libraries.jackson_core
    implementation(libraries.jackson_dataformat_yaml) {
        exclude group: "org.yaml", module: "snakeyaml"
    }
    implementation libraries.jackson_databind
    implementation libraries.jetty_client
    implementation libraries.jetty_http
    implementation libraries.jetty_io
    implementation libraries.jetty_util
    implementation libraries.jetty_websocket_api
    implementation libraries.jetty_websocket_client
    implementation libraries.jetty_websocket_common
    implementation libraries.spring_security_config
    implementation libraries.spring_security_core
    implementation libraries.spring_security_crypto
    implementation libraries.spring_security_web
    implementation libraries.tomcat_annotations_api
    implementation libraries.tomcat_embed_core
    implementation libraries.tomcat_embed_websocket
    implementation libraries.xstream
    implementation libraries.commons_io
    implementation libraries.guava
    implementation libraries.spring_aop
    implementation libraries.spring_beans
    implementation libraries.spring_context
    implementation libraries.spring_context_support
    implementation libraries.spring_core
    implementation libraries.spring_expression
    implementation libraries.spring_jcl
    implementation libraries.spring_messaging
    implementation libraries.spring_web
    implementation libraries.spring_webflux
    implementation libraries.spring_webmvc
    implementation libraries.spring_websocket
    implementation libraries.logback_core
    implementation libraries.logback_classic
    implementation libraries.apache_commons_lang3


    compileOnly libraries.lombok
    annotationProcessor libraries.lombok

    implementation libraries.netflix_servo

    testImplementation libraries.lombok
    testAnnotationProcessor libraries.lombok

    testImplementation(libraries.spring_boot_starter_test) {
        exclude group: "org.springframework", module: "spring-test"
    }
    testImplementation libraries.spring_test
    testImplementation libraries.json_smart
    testImplementation libraries.awaitility
    testImplementation libraries.rest_assured
    testImplementation(testFixtures(project(":integration-tests")))
}


bootJar {
    archiveFileName = "discovery-service.jar"
    manifest {
        attributes 'Main-Class': 'org.springframework.boot.loader.PropertiesLauncher'
    }
    archiveClassifier = "application"
}

jar {
    enabled true
}

bootRun {
    if (project.hasProperty('args')) {
        args project.args.split(',')
    }
    systemProperties = System.properties
}

publishing {
    publications {
        mavenJavaLib(MavenPublication) {
            artifact jar
        }

        mavenJavaFat(MavenPublication) {
            groupId = 'org.zowe.apiml.sdk'
            artifactId = 'discovery-fat-jar'

            artifact bootJar
        }
    }
}
