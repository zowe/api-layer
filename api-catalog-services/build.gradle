buildscript {
    repositories mavenRepositories
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:${gradleGitPropertiesVersion}")
    }
}

apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.gorylenko.gradle-git-properties'

springBoot {
    // This statement tells the Gradle Spring Boot plugin to generate a file
    // build/resources/main/META-INF/build-info.properties that is picked up by Spring Boot to display
    // via /info endpoint
    buildInfo {
        properties {
            // Generate extra build info:
            additionalProperties = [
                by: System.properties['user.name'],
                operatingSystem: "${System.properties['os.name']} (${System.properties['os.version']})",
                number: System.getenv('BUILD_NUMBER') ? System.getenv('BUILD_NUMBER') : "n/a",
                machine: InetAddress.localHost.hostName
            ]
        }
    }
}

gitProperties {
    gitPropertiesDir = new File("${project.rootDir}/${name}/build/resources/main/META-INF")
}

dependencies {
    compile(project(':common-service-core'))
    compile(project(':apiml-common'))
    compile(project(':integration-enabler-spring-v2'))
    compile(project(':security-service-client-spring'))

    compileOnly libraries.lombok

    compile libraries.spring_boot_starter_web
    compile libraries.spring_boot_starter_actuator
    compile libraries.spring_boot_configuration_processor
    compile libraries.spring_cloud_starter_eureka
    compile libraries.spring_boot_starter_webflux
    compile libraries.spring_retry
    compile libraries.swagger_core
    compile libraries.json_path
    compile libraries.apache_commons_lang3
    compile libraries.spring_boot_starter_thymeleaf
    compile libraries.apache_velocity

    testCompile libraries.rest_assured
    testCompile libraries.spring_mock_mvc
    testCompile libraries.lombok
    testCompile libraries.spring_boot_starter_test
}

bootJar.archiveName = "${bootJar.baseName}.jar"

def uiModule = "${project(':api-catalog-ui').projectDir}"

processResources {
    dependsOn "copyUi"
}

task copyUi(type: Copy) {
    group = "ui"

    dependsOn ":api-catalog-ui:build"

    inputs.dir(uiModule + '/frontend/build')
    outputs.dir('src/main/resources/static')

    from uiModule + '/frontend/build'
    into 'src/main/resources/static'
}
