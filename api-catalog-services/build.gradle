buildscript {
    repositories mavenRepositories
    dependencies {
        classpath libs.spring.boot.gradle.plugin
        classpath libs.gradle.git.properties
        classpath libs.jgit
    }
}

plugins {
    alias(libs.plugins.gradle.git.properties)
}

normalization {
    runtimeClasspath {
        ignore("**/*git.properties*")
        ignore("**/*build-info.properties*")
    }
}

apply plugin: 'org.springframework.boot'
apply from: "$projectDir/gradle/lite.gradle"
apply plugin: 'com.google.cloud.tools.jib'
apply from: "../gradle/jib.gradle"

def javaAgentPort = 6304
def debugPort = 5124
def componentName = "api-catalog-services"
if (project.hasProperty('zowe.jib.image.suffix')) { // Prefix for the component name (for the standalone catalog container)
    componentName += '-' + project.getProperty('zowe.jib.image.suffix')
}
if (project.hasProperty('zowe.jib.image.javaAgentPort')) {
    javaAgentPort = project.getProperty('zowe.jib.image.javaAgentPort')
}
if (project.hasProperty('zowe.jib.image.debugPort')) {
    debugPort = project.getProperty('zowe.jib.image.debugPort')
}

setJib(componentName, javaAgentPort, debugPort)

springBoot {
    // This statement tells the Gradle Spring Boot plugin to generate a file
    // build/resources/main/META-INF/build-info.properties that is picked up by Spring Boot to display
    // via /info endpoint
    buildInfo {
        properties {
            // Generate extra build info:
            additional = [
                by             : System.properties['user.name'],
                operatingSystem: "${System.properties['os.name']} (${System.properties['os.version']})",
                number         : System.getenv('BUILD_NUMBER') ? System.getenv('BUILD_NUMBER') : "n/a",
                machine        : InetAddress.localHost.hostName
            ]
        }
    }
}

gitProperties {
    gitPropertiesDir = new File("${project.rootDir}/${name}/build/resources/main/META-INF")
}

dependencies {
    api project(':apiml-tomcat-common')
    api project(':security-service-client-spring')

    implementation libs.spring.boot.starter.aop
    implementation libs.spring.boot.starter.actuator
    implementation libs.spring.boot.starter.security
    implementation libs.spring.boot.starter.web
    implementation libs.spring.cloud.starter.eureka.client
    implementation libs.spring.doc
    implementation libs.spring.retry

    implementation libs.apache.velocity

    implementation(libs.openapidiff) {
        exclude group: "io.swagger.core.v3", module: "swagger-core"
    }
    implementation(libs.swagger.inflector) {
        exclude group: "io.swagger.core.v3", module: "swagger-core"
    }

    testImplementation libs.spring.boot.starter.test
    testImplementation libs.spring.mock.mvc

    compileOnly libs.lombok
    annotationProcessor libs.lombok

    testCompileOnly libs.lombok
    testAnnotationProcessor libs.lombok
}

bootJar {
    archiveFileName = "api-catalog-services.jar"
    manifest {
        attributes 'Main-Class': 'org.springframework.boot.loader.launch.PropertiesLauncher'
    }
    archiveClassifier = "application"
}

jar {
    enabled true
    archiveClassifier = ""
}

def uiModule = "${project(':api-catalog-ui').projectDir}"

processResources {
    dependsOn "copyUi"
}

task copyUi(type: Copy) {
    group = "ui"

    dependsOn ":api-catalog-ui:build"

    inputs.dir(uiModule + '/frontend/build')
    outputs.dir('src/main/resources/static')

    from uiModule + '/frontend/build'
    into 'src/main/resources/static'
}

bootRun {
    if (project.hasProperty('args')) {
        args project.args.split(',')
    }

    debugOptions {
        port = 5014
        suspend = false
        server = true
    }

    workingDir = project.rootDir

    systemProperties = System.properties
}

publishing {
    publications {
        mavenJavaFat(MavenPublication) {
            groupId = 'org.zowe.apiml'
            artifactId = 'api-catalog-fat-jar'

            artifact bootJar
        }
    }
}
