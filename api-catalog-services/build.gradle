buildscript {
    repositories mavenRepositories
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:${gradleGitPropertiesVersion}")
    }
}

normalization {
    runtimeClasspath {
        ignore("**/*git.properties*")
        ignore("**/*build-info.properties*")
    }
}

apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.gorylenko.gradle-git-properties'
apply plugin: 'com.google.cloud.tools.jib'
apply from: "$projectDir/gradle/lite.gradle"
apply plugin: 'com.google.cloud.tools.jib'
apply from: "../gradle/jib.gradle"

def debugPort = 5124
def componentName = "api-catalog-services"

setJib(componentName, debugPort)

jib {
    to {
        image = project.hasProperty("zowe.docker.container") ? project.getProperty("zowe.docker.container") : "ghcr.io/zowe/api-catalog-services"
        auth {
            username = project.hasProperty("zowe.docker.username") ? project.getProperty("zowe.docker.username") : ""
            password = project.hasProperty("zowe.docker.password") ? project.getProperty("zowe.docker.password") : ""
        }
    }

    container {
        args = ['--spring.config.additional-location=file:/config/docker/api-catalog-service.yml', '--spring.profiles.include=dev,debug', '--apiml.security.ssl.nonStrictVerifySslCertificatesOfServices=true', '--apiml.security.ssl.verifySslCertificatesOfServices=false']
    }

    extraDirectories {
        paths {
            path {
                from = "../config"
                into = "/config"
            }
            path {
                from = "../keystore"
                into = "/keystore"
            }
        }
    }
}

springBoot {
    // This statement tells the Gradle Spring Boot plugin to generate a file
    // build/resources/main/META-INF/build-info.properties that is picked up by Spring Boot to display
    // via /info endpoint
    buildInfo {
        properties {
            // Generate extra build info:
            additionalProperties = [
                by: System.properties['user.name'],
                operatingSystem: "${System.properties['os.name']} (${System.properties['os.version']})",
                number: System.getenv('BUILD_NUMBER') ? System.getenv('BUILD_NUMBER') : "n/a",
                machine: InetAddress.localHost.hostName
            ]
        }
    }
}

gitProperties {
    gitPropertiesDir = new File("${project.rootDir}/${name}/build/resources/main/META-INF")
}

configurations.all {
    resolutionStrategy {
        force (libraries.jetty_websocket_client)
    }
}

dependencies {
    implementation(project(':security-service-client-spring'))

    compileOnly libraries.lombok
    annotationProcessor libraries.lombok

    implementation group: 'io.swagger', name: 'swagger-models', version: '1.6.2'
    implementation libraries.spring_boot_starter_web
    implementation libraries.spring_boot_starter_websocket
    implementation libraries.spring_boot_starter_actuator
    implementation libraries.spring_boot_configuration_processor
    implementation libraries.spring_cloud_starter_eureka
    implementation libraries.spring_retry
    implementation libraries.swagger_core
    implementation libraries.swagger3_core
    implementation libraries.swagger3_parser
    implementation libraries.jackson_core
    implementation libraries.apache_commons_lang3
    implementation libraries.spring_boot_starter_thymeleaf
    implementation libraries.apache_velocity
    implementation libraries.openapidiff
    implementation libraries.jetty_client
    implementation libraries.jetty_http
    implementation libraries.jetty_io
    implementation libraries.jetty_util
    implementation libraries.jetty_websocket_api
    implementation libraries.jetty_websocket_client
    implementation libraries.jetty_websocket_common

    implementation libraries.springFox

    testCompile libraries.rest_assured
    testCompile libraries.spring_mock_mvc

    testCompile libraries.lombok
    testAnnotationProcessor libraries.lombok

    testCompile libraries.spring_boot_starter_test
}

bootJar {
    archiveName "api-catalog-services.jar"
    manifest {
        attributes 'Main-Class': 'org.springframework.boot.loader.PropertiesLauncher'
    }
    archiveClassifier = "application"
}

jar {
    enabled true
}

def uiModule = "${project(':api-catalog-ui').projectDir}"

processResources {
    dependsOn "copyUi"
}

task copyUi(type: Copy) {
    group = "ui"

    dependsOn ":api-catalog-ui:build"

    inputs.dir(uiModule + '/frontend/build')
    outputs.dir('src/main/resources/static')

    from uiModule + '/frontend/build'
    into 'src/main/resources/static'
}

bootRun {
    if (project.hasProperty('args')) {
             args project.args.split(',')
    }
    systemProperties = System.properties
}

publishing {
    publications {
        mavenJavaLib(MavenPublication) {
            artifact jar
        }

        mavenJavaFat(MavenPublication) {
            groupId = 'org.zowe.apiml.sdk'
            artifactId = 'api-catalog-fat-jar'

            artifact bootJar
        }
    }
}
