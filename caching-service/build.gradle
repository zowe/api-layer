buildscript {
    repositories mavenRepositories
    dependencies {
        classpath libs.spring.boot.gradle.plugin
        classpath libs.jgit
    }
}

plugins {
    alias(libs.plugins.gradle.git.properties)
}

normalization {
    runtimeClasspath {
        ignore("**/*git.properties*")
        ignore("**/*build-info.properties*")
    }
}

apply plugin: 'org.springframework.boot'
apply plugin: 'com.google.cloud.tools.jib'
apply from: "../gradle/jib.gradle"

def javaAgentPort = 6306
def debugPort = 5126
def componentName = "caching-service"

setJib(componentName, javaAgentPort, debugPort)

springBoot {
    // This statement tells the Gradle Spring Boot plugin to generate a file
    // build/resources/main/META-INF/build-info.properties that is picked up by Spring Boot to display
    // via /info endpoint
    buildInfo {
        properties {
            // Generate extra build info:
            additional = [
                by: System.properties['user.name'],
                operatingSystem: "${System.properties['os.name']} (${System.properties['os.version']})",
                number: System.getenv('BUILD_NUMBER') ? System.getenv('BUILD_NUMBER') : "n/a",
                machine: InetAddress.localHost.hostName
            ]
        }
    }
}

gitProperties {
    gitPropertiesDir = new File("${project.rootDir}/${name}/build/resources/main/META-INF")
}

configurations.all {
    // Required because infinispan.jboss.marshalling and infinispan.lock are not compatible with Spring Boot 3
    exclude group: "org.infinispan", module: "infinispan-core"
    exclude group: "org.infinispan", module: "infinispan-commons"
}

dependencies {
    api project(':apiml-tomcat-common')
    api project(':onboarding-enabler-spring')

    implementation libs.spring.boot.starter.aop
    implementation libs.spring.boot.starter.security
    implementation libs.spring.boot.starter.actuator
    implementation libs.spring.doc
    implementation libs.spring.retry

    implementation libs.infinispan.spring.boot3.starter.embedded
    implementation libs.infinispan.jboss.marshalling
    implementation libs.infinispan.lock

    implementation libs.janino
    implementation libs.jakarta.servlet.api
    implementation libs.lettuce

    testImplementation(testFixtures(project(":integration-tests")))

    testImplementation libs.spring.boot.starter.test
    testImplementation libs.rest.assured

    compileOnly libs.lombok
    annotationProcessor libs.lombok
}

jar {
    enabled true
    archiveClassifier = ""
}

bootRun {
    if (project.hasProperty('args')) {
        args project.args.split(',')
    }

    debugOptions {
        port = 5016
        suspend = false
        server = true
    }

    workingDir = project.rootDir

    systemProperties = System.properties
}

bootJar.archiveFileName = bootJar.archiveBaseName.get() + ".jar"
