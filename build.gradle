import java.util.regex.Matcher

//noinspection GroovyAssignabilityCheck
group 'org.zowe.apiml'

buildscript {
    ext {
        licenseGradlePluginVerion = '0.13.1'
    }

    ext.mavenRepositories = {
        maven {
            url artifactoryMavenSnapshotRepo
            credentials {
                username mavenUser
                password mavenPassword
            }
        }
        maven {
            url artifactoryMavenRepo
            credentials {
                username mavenUser
                password mavenPassword
            }
        }
    }

    repositories mavenRepositories

    dependencies {
        classpath 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.7'
        classpath 'net.researchgate:gradle-release:2.7.0'
        classpath "gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:${licenseGradlePluginVerion}"
        classpath 'org.owasp:dependency-check-gradle:3.3.4'
    }
}

apply from: 'gradle/versions.gradle'
apply from: 'gradle/license.gradle'
apply from: 'gradle/publish.gradle'
apply from: 'gradle/sonar.gradle'
apply from: 'gradle/coverage.gradle'
apply from: 'gradle/code-quality.gradle'
apply from: 'gradle/generate-pom.gradle'
apply from: 'gradle/lite.gradle'

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'maven'
    apply plugin: 'com.github.hierynomus.license'
    apply plugin: 'org.owasp.dependencycheck'
    apply plugin: 'eclipse'

    repositories mavenRepositories

    sourceCompatibility = 1.8
    version = version

    idea {
        module {
            //noinspection GroovyAssignabilityCheck
            outputDir file('build/classes/main')
            //noinspection GroovyAssignabilityCheck
            testOutputDir file('build/classes/test')
            downloadJavadoc = true
            downloadSources = true
        }
    }
}

subprojects {
    license {
        header rootProject.file('.licence/EPL-2.0-licence-header.txt')
        ext.year = Calendar.getInstance().get(Calendar.YEAR)
        excludes(["**/*.yml", "**/*.yaml", "**/*.json", "**/static", "**/*.sh", "**/*.txt", "**/*.p12", "**/*.xml", "**/*.jsp", "**/*.html", "**/*.jks", "**/*.properties"])
        mapping {
            java = 'SLASHSTAR_STYLE'
        }
    }

    tasks.withType(Test) {
        maxParallelForks = 1
    }

    test {
        useJUnitPlatform()
    }

   dependencies {
       testImplementation libraries.junitJupiter
       testImplementation libraries.mockito_jupiter

       testImplementation  'org.junit.platform:junit-platform-launcher:1.6.0'

       //For Idea
       testImplementation  'org.junit.platform:junit-platform-commons:1.6.0'
       testImplementation  'org.junit.platform:junit-platform-engine:1.6.0'
   }
}

configurations {
    all*.exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    all*.exclude group: 'com.fasterxml.jackson.module', module: 'jackson-module-kotlin'
}

task buildCore(dependsOn: [':gateway-service:build', ':discovery-service:build', ':api-catalog-services:build', ':api-catalog-ui:build',
                           ':discoverable-client:build', ':zaas-client:build']) {
    description "Build core components"
    group "build"
}

task runIntegrationTests(dependsOn: ":integration-tests:runIntegrationTests") {
    description "Run integration tests"
    group "Integration tests"
}

task runAllIntegrationTests(dependsOn: ":integration-tests:runAllIntegrationTests") {
    description "Run all integration tests"
    group "Integration tests"
}

task environmentCheck(dependsOn: ":integration-tests:environmentCheck") {
    description "Environment check"
    group "Integration tests"
}

task runIntegrationTestInstances(dependsOn: buildCore) {
    doFirst {
        if (System.properties['os.name'].toLowerCase().contains('windows')) {
            ext.process = new ProcessBuilder()
                .directory(projectDir)
                .command("npm.cmd", "run", "api-layer-ci")
                .start()
        } else {
            ext.process = new ProcessBuilder()
                .directory(projectDir)
                .command("npm", "run", "api-layer-ci", ">", "integration-instances.log")
                .start()
        }
    }
}

task runCITests(dependsOn: [":integration-tests:runCITests", "runIntegrationTestInstances"]) {
    description "Run Integration Test Without MF Dependencies"
    group "Integration tests"
}

task runCITestsInternalPort(dependsOn: [":integration-tests:runCITestsInternalPort"]) {
    description "Run Integration Test Without MF Dependencies, this is meant to be run after runCITests so the apiml is not automatically started here"
    group "Integration tests"
}

task runMainFrameTests(dependsOn: ":integration-tests:runMainFrameTests") {
    description "Run only tests related to the MainFrame"
    group "Integration tests"
}

task publishAllVersions {
    group 'Zowe Publishing'
    description 'Publish ZIP file and SDK libraries to Zowe Artifactory'
    doLast {
        println 'Published ZIP file and libraries'
    }
}

task nodejsClean(type: Delete) {
    group = 'npm'
    if (cleanNode == 'true') {
        delete 'api-catalog-ui/tools/nodejs', 'api-catalog-ui/tools/npm', 'api-catalog-ui/tools/yarn'
        delete 'onboarding-enabler-nodejs-sample-app/tools/nodejs', 'onboarding-enabler-nodejs-sample-app/tools/npm', 'onboarding-enabler-nodejs-sample-app/tools/yarn'
    }
}
clean.dependsOn nodejsClean

publishAllVersions.dependsOn publishSdkArtifacts

//-----------Release part start
apply plugin: 'net.researchgate.release'

ext.releaseScope = project.hasProperty('release.scope') ? project.getProperty('release.scope') : 'patch'

release {
    failOnCommitNeeded = true
    failOnPublishNeeded = true
    failOnSnapshotDependencies = true
    failOnUnversionedFiles = true
    failOnUpdateNeeded = true
    revertOnFail = true
    preCommitText = '[Gradle Release plugin]'
    preTagCommitMessage = 'Before tag commit'
    tagCommitMessage = 'Release:'
    tagTemplate = 'v${version}'
    newVersionCommitMessage = 'Create new version:'
    versionPropertyFile = 'gradle.properties'

    if (releaseScope == 'minor') {
        versionPatterns = [
            /[.]*\.(\d+)\.(\d+)[.]*/: { Matcher m, Project p -> m.replaceAll(".${(m[0][1] as int) + 1}.0") }
        ]
    } else if (releaseScope == 'major') {
        versionPatterns = [
            /(\d+)\.(\d+)\.(\d+)[.]*/: { Matcher m, Project p -> m.replaceAll("${(m[0][1] as int) + 1}.0.0") }
        ]
    } else {
        versionPatterns = [
            /(\d+)([^\d]*$)/: { Matcher m, Project p -> m.replaceAll("${(m[0][1] as int) + 1}${m[0][2]}") }
        ]
    }

    scmAdapters = [
        net.researchgate.release.GitAdapter
    ]

    git {
        requireBranch = 'master'
        pushToRemote = 'origin'
        pushToBranchPrefix = ''
        commitVersionFileOnly = false
        signTag = false
    }
}

afterReleaseBuild.dependsOn publishAllVersions
//-----------Release part end


if (hasProperty('buildScan')) {
    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }
}
