ext {
    artifactName = 'api-catalog-package'
    pullNo = project.hasProperty("pullRequest") && project.getProperty("pullRequest") != null ? "-" + project.getProperty("pullRequest") : ""
}

configurations {
    catalogServiceJar
    zip
}

dependencies {
    catalogServiceJar(project(path: ":api-catalog-services", configuration: "liteJarConfiguration"))
}

task packageApiCatalog(type: Zip) {
    archiveName = artifactName + pullNo + ".zip"
    includeEmptyDirs = true
    def resourceDir = 'src/main/resources/'

    into('/') {
        from "$resourceDir/manifest.yaml"
        from "$resourceDir/pluginDefinition.json"
    }

    into('plugin/') {
        from "$resourceDir/plugin/pluginDefinition.json"
    }

    into('plugin/web/assets/') {
        from "$resourceDir/plugin/web/assets/api-catalog.png"
    }

    into('bin/') {
        from configurations.catalogServiceJar
        from "$resourceDir/bin/start.sh"
        from "$resourceDir/bin/configure.sh"
        from "$resourceDir/bin/validate.sh"
    }
}

jar.dependsOn(packageApiCatalog)
build.dependsOn(packageApiCatalog)

artifacts {
    zip packageApiCatalog
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact("$buildDir/distributions/$artifactName" + pullNo + ".zip")
        }
    }
}

jar {
    enabled false
}
