plugins {
    id 'java-platform'
    id 'maven-publish'
}

ext {
    artifactId = 'api-layer-bom'
}

javaPlatform {
    allowDependencies()
}

/**
 * all transitive dependencies upgrades in the whole project
 * to use in other module use api(platform(project(":platform")))
 */
dependencies {
    api(platform(libs.spring.boot.dependencies))
    api(platform(libs.netty.bom))
    api(platform(libs.aws.bom))
    api(platform(libs.micronaut.bom))

    constraints {
        // to define preferred version (the minimal version constraints)
        libs.getCommons().owner.config.libraries.values().forEach { x ->
            def version = x.getVersion().getPreferredVersion()
            if (version.isEmpty()) version = x.getVersion().toString()
            api x.getGroup().toString() + ":" + x.getName().toString() + ":" + version
        }
    }
}

publishing {
    repositories.maven {
        credentials {
            username project.hasProperty("zowe.deploy.username") ? project.getProperty("zowe.deploy.username") : ""
            password project.hasProperty("zowe.deploy.password") ? project.getProperty("zowe.deploy.password") : ""
        }
        if (rootProject.version.endsWith("-SNAPSHOT")) {
            setUrl(artifactoryPublishingMavenSnapshotRepo)
        } else {
            setUrl(artifactoryPublishingMavenRepo)
        }
    }

    publications {
        mavenJava(MavenPublication) {
            pom {
                licenses {
                    license {
                        name = 'Eclipse Public License, v2.0'
                        url = 'https://www.eclipse.org/legal/epl-2.0/'
                    }
                }
            }

            groupId 'org.zowe.apiml'
            version rootProject.version
            artifactId "${project.artifactId}"
        }
    }
}
