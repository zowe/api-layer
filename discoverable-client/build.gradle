buildscript {
    repositories mavenRepositories
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:${gradleGitPropertiesVersion}")
    }
}

normalization {
    runtimeClasspath {
        ignore("**/*git.properties*")
        ignore("**/*build-info.properties*")
    }
}

apply plugin: 'org.springframework.boot'
apply plugin: 'com.gorylenko.gradle-git-properties'
apply plugin: 'com.google.cloud.tools.jib'

jib {
    to {
        image = project.hasProperty("zowe.docker.container") ? "${project.getProperty("zowe.docker.container")}discoverable-client" : "ghcr.io/zowe/discoverable-client"
        auth {
            username = project.hasProperty("zowe.docker.username") ? project.getProperty("zowe.docker.username") : ""
            password = project.hasProperty("zowe.docker.password") ? project.getProperty("zowe.docker.password") : ""
        }
    }

    container {
        args = ['--spring.config.additional-location=file:/config/docker/discoverable-client.yml', '--spring.profiles.include=dev,debug']
    }

    extraDirectories {
        paths {
            path {
                from = "../config"
                into = "/config"
            }
            path {
                from = "../keystore"
                into = "/keystore"
            }
        }
    }
}

springBoot {
    // This statement tells the Gradle Spring Boot plugin to generate a file
    // build/resources/main/META-INF/build-info.properties that is picked up by Spring Boot to display
    // via /info endpoint
    buildInfo {
        properties {
            // Generate extra build info:
            additionalProperties = [
                by: System.properties['user.name'],
                operatingSystem: "${System.properties['os.name']} (${System.properties['os.version']})",
                number: System.getenv('BUILD_NUMBER') ? System.getenv('BUILD_NUMBER') : "n/a",
                machine: InetAddress.localHost.hostName
            ]
        }
    }
}

gitProperties {
    gitPropertiesDir = new File("${project.rootDir}/${name}/build/resources/main/META-INF")
}

dependencies {
    implementation project(':onboarding-enabler-spring')
    implementation project(':zaas-client')
    implementation('javax.servlet:javax.servlet-api') {
        version {
            strictly '3.1.0'
        }
    }
    implementation libraries.springFox
    implementation libraries.spring_boot_starter
    implementation libraries.spring_boot_starter_actuator
    implementation libraries.spring_boot_starter_web
    implementation libraries.spring_boot_starter_websocket
    implementation libraries.spring_boot_starter_validation
    implementation libraries.spring_boot_starter_security

    implementation libraries.bootstrap
    implementation libraries.jquery

    implementation libraries.gson
    compileOnly libraries.lombok
    annotationProcessor libraries.lombok

    testImplementation libraries.spring_boot_starter_test
}


bootJar.archiveName = "${bootJar.baseName}.jar"

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact bootJar
        }
    }
}
